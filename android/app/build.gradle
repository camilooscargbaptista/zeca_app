plugins {
    id "com.android.application"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    // END: FlutterFire Configuration
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

// flutter_background_geolocation - REMOVIDO (incompatibilidade + permissões sensíveis)
// Project background_geolocation = project(':flutter_background_geolocation')
// apply from: "${background_geolocation.projectDir}/background_geolocation.gradle"

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.zeca.app"
    compileSdk 36
    ndkVersion "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.zeca.app"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        
        // Injetar variáveis de forma segura via manifestPlaceholders
        def googleMapsApiKey = localProperties.getProperty('GOOGLE_MAPS_API_KEY', '')
        manifestPlaceholders = [
            GOOGLE_MAPS_API_KEY: googleMapsApiKey,
            applicationName: "io.flutter.embedding.android.FlutterApplication"
        ]
    }

    // Configuração de assinatura para release (Google Play)
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file('key.properties')
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false   // <-- REQUIRED for flutter_background_geolocation
        }
    }
    
    // Configuração de splits para App Bundle (resolve erro de pacotes de apps)
    bundle {
        language {
            enableSplit = false  // Incluir todos os idiomas em um único pacote
        }
        density {
            enableSplit = false  // Incluir todas as densidades em um único pacote
        }
        abi {
            enableSplit = true  // Separar por ABI (arm64-v8a, x86_64)
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation platform('com.google.firebase:firebase-bom:32.7.0')
    implementation 'com.google.firebase:firebase-messaging'
}

